local branch="$(git rev-parse --abbrev-ref HEAD)"

#local out="$(anyframe-source-git-commit-log $branch \
local out="$(git log --graph --decorate --oneline --color -300 $branch \
  | fzf --header="$branch (ctrl-t: show, ctrl-v: insert, ctrl-x: github)" \
        --bind="${FZF_PREVIEW_KEYBIND}" \
        --expect="ctrl-t,ctrl-v,ctrl-x" \
        --preview="preview-git-commit {}")"

local key="$(head -1 <<< $out)"
local lines="$(tail -n +2 <<< $out)"
local line="$(head -1 <<< $lines)"

local commit="$(sed -n 's/^[^a-f0-9]\{0,\}\([a-f0-9]\{7,10\}\).\{1,\}$/\1/p' <<< $line)"

if test -z "$commit"; then
  zle reset-prompt
  return 0
fi

case $key in
ctrl-v)
  anyframe-action-insert <<< $commit
  ;;
ctrl-t)
  anyframe-action-execute git show --color <<< $commit
  ;;
ctrl-x)
  local giturl="$(git remote get-url origin)"
  giturl=${giturl/git\@github\.com\:/https://github.com/}
  giturl=${giturl%\.git}

  anyframe-action-execute " open" <<< "${giturl}/commit/${commit}"
  ;;
*)
  anyframe-source-git-commit-actions $commit \
    | fzf --header="$commit" \
    | anyframe-action-execute
  ;;
esac

zle reset-prompt

# Local Variables:
# mode: Shell-Script
# End:
# vim: ft=zsh
